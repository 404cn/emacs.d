* Packages

#+begin_src elisp
  (straight-use-package 'session)
  (straight-use-package 'super-save)
#+end_src

* Autoloads

#+begin_src elisp
  (defvar desktop-base-file-name)
  (defvar desktop-dirname)
  (defvar desktop-restore-eager)
  (defvar desktop-file-modtime)

  ;;
  ;;; Helpers

  ;;;###autoload
  (defun nasy-session-file (&optional name)
    "TODO"
    (cond ((require 'persp-mode nil t)
           (expand-file-name (or name persp-auto-save-fname) persp-save-dir))
          ((require 'desktop nil t)
           (if name
               (expand-file-name name (file-name-directory (desktop-full-file-name)))
             (desktop-full-file-name)))
          ((error "No session backend available"))))

  ;;;###autoload
  (defun nasy-save-session (&optional file)
    "TODO"
    (setq file (expand-file-name (or file (nasy-session-file))))
    (cond ((require 'persp-mode nil t)
           (unless persp-mode (persp-mode +1))
           (setq persp-auto-save-opt 0)
           (persp-save-state-to-file file))
          ((and (require 'frameset nil t)
                (require 'restart-emacs nil t))
           (let ((frameset-filter-alist (append '((client . restart-emacs--record-tty-file))
                                                frameset-filter-alist))
                 (desktop-base-file-name (file-name-nondirectory file))
                 (desktop-dirname (file-name-directory file))
                 (desktop-restore-eager t)
                 desktop-file-modtime)
             (make-directory desktop-dirname t)
             ;; Prevents confirmation prompts
             (let ((desktop-file-modtime (nth 5 (file-attributes (desktop-full-file-name)))))
               (desktop-save desktop-dirname t))))
          ((error "No session backend to save session with"))))

  ;;;###autoload
  (defun nasy-load-session (&optional file)
    "TODO"
    (setq file (expand-file-name (or file (nasy-session-file))))
    (message "Attempting to load %s" file)
    (cond ((not (file-readable-p file))
           (message "No session file at %S to read from" file))
          ((require 'persp-mode nil t)
           (unless persp-mode
             (persp-mode +1))
           (let ((allowed (persp-list-persp-names-in-file file)))
             (cl-loop for name being the hash-keys of *persp-hash*
                      unless (member name allowed)
                      do (persp-kill name))
             (persp-load-state-from-file file)))
          ((and (require 'frameset nil t)
                (require 'restart-emacs nil t))
           (restart-emacs--restore-frames-using-desktop file))
          ((error "No session backend to load session with"))))


  ;;
  ;;; Command line switch

  ;;;###autoload
  (defun nasy-restore-session-handler (&rest _)
    "TODO"
    (add-hook 'window-setup-hook #'nasy-load-session 'append))


  ;;
  ;;; Commands

  ;;;###autoload
  (defun nasy/quickload-session ()
    "TODO"
    (interactive)
    (message "Restoring session...")
    (nasy-load-session)
    (message "Session restored. Welcome back."))

  ;;;###autoload
  (defun nasy/quicksave-session ()
    "TODO"
    (interactive)
    (message "Saving session")
    (nasy-save-session)
    (message "Saving session...DONE"))

  ;;;###autoload
  (defun nasy/load-session (file)
    "TODO"
    (interactive
     (let ((session-file (nasy-session-file)))
       (list (or (read-file-name "Session to restore: "
                                 (file-name-directory session-file)
                                 (file-name-nondirectory session-file)
                                 t)
                 (user-error "No session selected. Aborting")))))
    (unless file
      (error "No session file selected"))
    (message "Loading '%s' session" file)
    (nasy-load-session file)
    (message "Session restored. Welcome back."))

  ;;;###autoload
  (defun nasy/save-session (file)
    "TODO"
    (interactive
     (let ((session-file (nasy-session-file)))
       (list (or (read-file-name "Save session to: "
                                 (file-name-directory session-file)
                                 (file-name-nondirectory session-file))
                 (user-error "No session selected. Aborting")))))
    (unless file
      (error "No session file selected"))
    (message "Saving '%s' session" file)
    (nasy-save-session file))

  ;;;###autoload
  (defalias 'nasy/restart #'restart-emacs)

  ;;;###autoload
  (defun nasy/restart-and-restore (&optional debug)
    "TODO"
    (interactive "P")
    (setq nasy-autosave-session nil)
    (nasy/quicksave-session)
    (restart-emacs
     (append (if debug (list "--debug-init"))
             (when (boundp 'chemacs-current-emacs-profile)
               (list "--with-profile" chemacs-current-emacs-profile))
             (list "--restore"))))
#+end_src

* Configs

#+begin_src elisp
  (gsetq kill-ring-max 300)

  (gsetq history-length 3000
         history-delete-duplicates t
         savehist-additional-variables
         '(mark-ring
           global-mark-ring
           search-ring
           regexp-search-ring
           extended-command-history)
         savehist-autosave-interval 60)

  (add-hook #'after-init-hook #'savehist-mode)

  (use-package session
    :defer    t
    :hook ((after-init . session-initialize))
    :init
    (gsetq session-save-file (no-littering-expand-var-file-name ".session")
           session-name-disable-regexp "\\(?:\\`'/tmp\\|\\.git/[A-Z_]+\\'\\)"
           session-save-file-coding-system 'utf-8
           desktop-globals-to-save
           (append '((comint-input-ring        . 50)
                     (compile-history          . 30)
                     desktop-missing-file-warning
                     (dired-regexp-history     . 20)
                     (extended-command-history . 30)
                     (face-name-history        . 20)
                     (file-name-history        . 100)
                     (grep-find-history        . 30)
                     (grep-history             . 30)
                     (ivy-history              . 100)
                     (magit-revision-history   . 50)
                     (minibuffer-history       . 50)
                     (org-clock-history        . 50)
                     (org-refile-history       . 50)
                     (org-tags-history         . 50)
                     (query-replace-history    . 60)
                     (read-expression-history  . 60)
                     (regexp-history           . 60)
                     (regexp-search-ring       . 20)
                     register-alist
                     (search-ring              . 20)
                     (shell-command-history    . 50)
                     tags-file-name
                     tags-table-list
                     kill-ring))))
#+end_src

* Auto Save (Super Save)

#+begin_src elisp :exports none
  ;;----------------------------------------------------------------------------
  ;; Auto Save
  ;; I use super save to auto save files.
#+end_src

I use super save to auto save files.

#+begin_src elisp
  (use-package super-save
    :ghook 'after-init-hook
    :gfhook '(lambda () (remove-hook #'mouse-leave-buffer-hook #'super-save-command))
    :init (gsetq super-save-auto-save-when-idle nil
                 super-save-remote-files        nil
                 super-save-hook-triggers       nil
                 super-save-triggers
                 '(ibuffer other-window windmove-up windmove-down windmove-left windmove-right next-buffer previous-buffer)))
#+end_src
